plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.4.32'
    id 'com.android.library'
    id 'kotlin-android-extensions'
    id 'maven-publish'
}

group = 'nz.ac.aut.sincmaths'
version = '0.1'

repositories {
    google()
    jcenter()
}

kotlin {
    android()
    iosX64('ios') {
        compilations.main.cinterops {
            tinyexpr {
                defFile "$projectDir/src/nativeInterop/cinterop/tinyexpr.def"
                includeDirs "$projectDir/src/cpp/tinyexpr/include"
            }
            wavelib {
                defFile "$projectDir/src/nativeInterop/cinterop/wavelib.def"
                includeDirs "$projectDir/src/cpp/wavelib/include"
            }
        }
        binaries {
            framework {
                baseName = 'SincMaths'
            }
        }
    }
    sourceSets {
        commonMain {
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                implementation 'org.ejml:ejml-simple:0.40'
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation 'androidx.test.espresso:espresso-core:3.3.0'
            }
            resources.srcDirs = ['src/androidTest/resources/']
        }
        iosMain {
        }
        iosTest {
        }
    }
}

android {
    defaultConfig {
        ndkVersion "21.3.6528147"
        buildToolsVersion "30.0.0"
        compileSdkVersion 30
        targetSdkVersion 30
        minSdkVersion 16

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path file("src/cpp/CMakeLists.txt")
        }
    }

    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
}

tasks.iosProcessResources {
    copy {
        from "$projectDir/src/androidTest/resources/"
        into "$projectDir/build/bin/ios/debugTest"
        include '*.csv'
    }
}