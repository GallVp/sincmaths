plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.6.10'
    id 'com.android.library'
    id 'kotlin-android-extensions'
    id 'maven-publish'
    id("org.jetbrains.dokka") version "1.6.10"
    id "org.jetbrains.kotlin.native.cocoapods" version "1.6.10"
}

group = 'com.github.gallvp'
version = '0.2'

repositories {
    google()
    mavenCentral()
}

import org.jetbrains.kotlin.gradle.tasks.FatFrameworkTask

kotlin {
    android {
        publishAllLibraryVariants()
    }
    ios {
        compilations.main.cinterops {
            tinyexpr {
                defFile "$projectDir/src/nativeInterop/cinterop/tinyexpr.def"
                includeDirs "$projectDir/src/cpp/tinyexpr/include"
            }
            wavelib {
                defFile "$projectDir/src/nativeInterop/cinterop/wavelib.def"
                includeDirs "$projectDir/src/cpp/wavelib/include"
            }
        }

        binaries {
            framework {
                baseName = 'SincMaths'
            }
        }
    }

    cocoapods {
        summary = "SincMaths Library"
        homepage = "https://github.com/GallVp/SincMathsMP"
        authors = "Usman Rashid"
        ios.deploymentTarget = "13.0"

        frameworkName = "SincMaths"
    }

    tasks.register("debugFatFramework", FatFrameworkTask) {
        baseName = "SincMaths"
        destinationDir = file("$buildDir/fat-framework/debug")
        from(
                targets.iosX64.binaries.getFramework("DEBUG"),
                targets.iosArm64.binaries.getFramework("DEBUG")
        )
    }

    tasks.register("releaseFatFramework", FatFrameworkTask) {
        baseName = "SincMaths"
        destinationDir = file("$buildDir/fat-framework/release")
        from(
                targets.iosX64.binaries.getFramework("RELEASE"),
                targets.iosArm64.binaries.getFramework("RELEASE")
        )
    }

    sourceSets {
        commonMain {
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                implementation 'org.ejml:ejml-simple:0.41'
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation 'androidx.test.espresso:espresso-core:3.4.0'
            }
            resources.srcDirs = ['src/androidTest/resources/']
        }
        iosMain {
        }
        iosTest {
        }
    }
}

android {
    defaultConfig {
        ndkVersion "23.0.7599858"
        compileSdkVersion 31
        targetSdkVersion 31
        minSdkVersion 16

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path file("src/cpp/CMakeLists.txt")
        }
    }

    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
}

tasks.iosX64Test {
    doFirst {
        copy {
            from "$projectDir/src/androidTest/resources/sincmaths"
            into "$projectDir/build/bin/iosX64/debugTest"
            include "*.csv"
        }
    }
}